buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'idea'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

version = '1.2.1'
def jarVersion = version

allprojects {
    apply plugin: 'jacoco'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'org.cf'
    sourceCompatibility = 1.8
    version = parent.version

    jar {
        version = jarVersion
    }

    ext {
        depends = [
            commons_lang   : 'org.apache.commons:commons-lang3:3.9',
            findbugs       : 'com.google.code.findbugs:jsr305:3.0.1',
            guava          : 'com.google.guava:guava:28.2-jre',
            junit          : 'junit:junit:4.13',
            mockito        : 'org.mockito:mockito-core:1.10.19',
            perfidix       : 'org.perfidix:perfidix:3.6.9',
            proguard_gradle: 'net.sf.proguard:proguard-gradle:6.2.2',
            slf4j_api      : 'org.slf4j:slf4j-api:1.7.30',
            slf4j_nop      : 'org.slf4j:slf4j-nop:1.7.30',
            trove4j        : 'net.sf.trove4j:trove4j:3.0.3',
        ]
    }

    if (project.name != 'demoapp') {
        jacocoTestReport {
            reports {
                html.enabled = true
                xml.enabled = true
                csv.enabled = false
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.fork = true
        //options.incremental = true
    }

    if (project.name == 'simplify') {
        tasks.withType(Test) {
            maxParallelForks = 2
            forkEvery = 4
            minHeapSize = '64m'
        }
    } else {
        tasks.withType(Test) {
            maxParallelForks = Runtime.runtime.availableProcessors()
        }
    }
}


// Demoapp isn't intended to have tests and shouldn't count against code coverage
def reportProjects = subprojects - project(':demoapp')

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(reportProjects.test)

    sourceDirectories = files(reportProjects.sourceSets.main.allJava.srcDirs)
    classDirectories = files(reportProjects.sourceSets.main.output.classesDirs)
    executionData = files(reportProjects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}


coveralls {
    sourceDirs = reportProjects.sourceSets.main.allJava.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}


if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(Test) {
            testLogging {
                // Print out test status to make it easier to debug problematic builds.
                events "started", "passed", "skipped", "failed"
            }

            /*
             * Tests are memory intensive and TravisCI only has 3-4 GB available.
             * Gradle creates a lot of forks, so keep fork min heap size low.
             * Also, fork frequently to get a fresh heap in case of memory leaks.
             * Using larger values doesn't seem to improve test performance.
            */
            maxParallelForks = 2
            minHeapSize = '64m'
            forkEvery = 4
        }
    }
}
